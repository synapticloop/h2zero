plugins {
    id "java"
    id "eclipse"
    id "maven-publish"

    id "com.github.ben-manes.versions" version "0.49.0"

    id "com.gradle.plugin-publish" version "1.2.1"

    id "synapticloop.copyrightr" version "1.3.1"

}

version = "5.0.0"
group = 'synapticloop'

tasks.withType(Javadoc).all { enabled = false }

allprojects {
    tasks.withType(Javadoc).all { enabled = false }
}
repositories {
    mavenLocal()
    mavenCentral()
    maven {
        url "https://jitpack.io"
    }

}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

dependencies {
    implementation "com.github.synapticloop:templar:1.5.5"
    implementation "org.json:json:20231013"
    implementation "com.mchange:c3p0:0.9.5.5"
    implementation "commons-validator:commons-validator:1.7"
    implementation "org.slf4j:slf4j-api:1.7.26"
    implementation "javax.mail:javax.mail-api:1.6.2"
    implementation "org.apache.ant:ant:1.10.14"

    compileOnly gradleApi()

    testImplementation "junit:junit:4.13.2"
    testImplementation "org.mockito:mockito-core:5.6.0"
    testImplementation "com.github.stefanbirkner:system-rules:1.17.1"
}

configurations.all {
    resolutionStrategy {
        failOnVersionConflict()
        force "junit:junit:4.13.2"
    }
}

jar {
    manifest {
        attributes(
                "Main-Class": "synapticloop.h2zero.Main",
                "Implementation-Version": version
        )
    }
}

test {
    include "**/*Test.class"
    maxParallelForks = 1
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId "synapticloop"
            artifactId "h2zero"
            version version

            from components.java
        }
    }
}

/*
 * The Gradle Plugin Publisher
 */
gradlePlugin {
    website = "https://github.com/synapticloop/h2zero"
    vcsUrl = "https://github.com/synapticloop/h2zero"

    plugins {
        h2zeroPlugin {
            id = "synapticloop.h2zero"
            implementationClass = "synapticloop.h2zero.plugin.gradle.H2ZeroPlugin"
            displayName = "Synapticloop Gradle h2zero plugin"
            description = "lightweight ORM Java code generator for SQL databases: mysql, sqlite3, postgresql, and cockroach - this utilises the templar templating language"
            tags = ["h2zero", "ORM", "generator"]
        }
    }
}

copyrightr {
    dryRun = false
    copyrightHolder = "synapticloop"
    includes = [
            "src/**/*.java",
            "src/**/*.groovy"
    ]

    excludes = [
            "src/test/java/**/*.java",
            "**/*.class"
    ]

    onlyReplaceFirst = true

    yearSeparator = " - "

    failOnMissing = false
}

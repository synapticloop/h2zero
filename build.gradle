plugins {
	id 'java'
	id 'eclipse'
	id "maven"
	id "maven-publish"

	id "com.github.ben-manes.versions" version "0.17.0"
	id "com.github.johnrengelman.shadow" version "2.0.2"
	id "com.jfrog.bintray" version "1.8.0"

	id 'com.gradle.plugin-publish' version '0.9.10'
	id 'co.riiid.gradle' version '0.4.2'
	id 'net.saliman.cobertura' version '2.5.4'

	id 'synapticloop.copyrightr' version '1.1.2'
	id 'synapticloop.documentr' version '2.9.0'
}

group = 'synapticloop'
archivesBaseName = 'h2zero'
description = """lightweight ORM generator for mysql, java and optionally jsp/servlets"""
version = '2.0.0'

tasks.withType(Javadoc).all { enabled = false }

sourceCompatibility = 1.7
targetCompatibility = 1.7

repositories {
	mavenLocal()
	mavenCentral()
	jcenter()
}
configurations {
	shadowRuntime
}

dependencies {
	runtime 'synapticloop:templar:1.4.4'
	runtime 'org.json:json:20180130'
	runtime 'com.mchange:c3p0:0.9.5.2'
	runtime 'commons-validator:commons-validator:1.6'
	runtime 'org.slf4j:slf4j-api:1.7.25'
	runtime 'io.dropwizard.metrics:metrics-core:3.1.2'
	runtime 'javax.mail:javax.mail-api:1.6.1'

	compile gradleApi()
	compile 'synapticloop:templar:1.4.4'
	compile 'org.json:json:20180130'
	compile 'org.apache.ant:ant:1.10.2'
	compile 'javax.servlet.jsp:jsp-api:2.2'
	compile 'javax.servlet:javax.servlet-api:3.1.0'
	compile 'com.mchange:c3p0:0.9.5.2'
	compile 'commons-validator:commons-validator:1.6'

	compile 'org.slf4j:slf4j-api:1.7.25'
	compile 'org.apache.logging.log4j:log4j-slf4j-impl:2.10.0'
	compile 'org.apache.logging.log4j:log4j-core:2.10.0'

	compile 'io.dropwizard.metrics:metrics-core:3.1.2'
	compile 'javax.mail:javax.mail-api:1.6.1'
	compile 'com.github.stefanbirkner:system-rules:1.17.1'

	shadowRuntime 'synapticloop:templar:1.4.4'
	shadowRuntime 'org.json:json:20180130'
	shadowRuntime 'org.apache.ant:ant:1.10.2'
	

	testRuntime 'junit:junit:4.12'
	testRuntime 'org.mockito:mockito-all:1.10.19'
	testRuntime 'com.github.stefanbirkner:system-rules:1.16.1'
	testRuntime 'mysql:mysql-connector-java:6.0.6'
	testRuntime 'org.xerial:sqlite-jdbc:3.21.0.1'

	testCompile 'junit:junit:4.12'
	testCompile 'org.mockito:mockito-all:1.10.19'
	testCompile 'com.github.stefanbirkner:system-rules:1.16.1'
}

jar {
	manifest {
		attributes("Main-Class": "synapticloop.h2zero.Main",
				"Implementation-Version": version)
	}
}

cobertura {
	coverageFormats = [ 'html', 'xml']
	coverageDirs = [
		project.sourceSets.main.output.classesDir,
		file("build/classes/test/")
	]
	coverageSourceDirs = [ 
		project.sourceSets.main.java.srcDirs, 
		file("src/test/java/")
	]
}

test {
	include '**/*Test.class'
	maxParallelForks = 1
}

shadowJar {
	configurations = [project.configurations.shadowRuntime]
}

dependencyUpdates.resolutionStrategy = {
	componentSelection { rules ->
		rules.all { ComponentSelection selection ->
			boolean rejected = ['alpha', 'beta', 'rc', 'cr', 'm', 'b', 'dmr'].any { qualifier ->
				selection.candidate.version ==~ /(?i).*[.-]${qualifier}[.\d-]*/
			}
			if (rejected) {
				selection.reject('Release candidate')
			}
		}
	}
}

def javaApiUrl = 'http://docs.oracle.com/javase/1.7.0/docs/api/'
def groovyApiUrl = 'http://groovy.codehaus.org/gapi/'

tasks.withType(Javadoc) {
	options.links(javaApiUrl, groovyApiUrl)
}

task javadocJar(type: Jar, dependsOn: javadoc) {
	classifier = 'javadoc'
	from 'build/docs/javadoc'
}

task sourcesJar(type: Jar) {
	from sourceSets.main.allSource
	classifier = 'sources'
}

build.finalizedBy(project.tasks.shadowJar)
test.finalizedBy(project.tasks.cobertura)


publishing {
	publications {
		Synapticloop(MavenPublication) {
			from components.java
			artifact sourcesJar
			artifact javadocJar

			groupId 'synapticloop'

			artifactId 'h2zero'

			pom.withXml {
				configurations.compile.resolvedConfiguration.firstLevelModuleDependencies.each { dep ->
					asNode().dependencies[0].dependency.find {
						it.artifactId[0].text() == dep.moduleName &&
								it.groupId[0].text() == dep.moduleGroup
					}.scope[0].value = 'compile'
				}
			}
		}
	}
}

bintray {
	user = System.getenv('BINTRAY_USER')
	key = System.getenv('BINTRAY_PASSWORD')

	publications = [ 'Synapticloop' ]

	publish = true

	pkg {
		repo = 'maven'
		name = 'h2zero'
	}

}

github {
	owner = group
	repo = archivesBaseName
	if(System.getenv('GITHUB_TOKEN')) {
		token = System.getenv('GITHUB_TOKEN')
	}
	tagName = version
	name = version
	assets = [
		'build/libs/' + archivesBaseName + '-' + version + '.jar',
		'build/libs/' + archivesBaseName + '-' + version + '-all.jar'
	]
}

pluginBundle {

	website = 'https://github.com/synapticloop/h2zero'
	vcsUrl = 'https://github.com/synapticloop/h2zero'
	description = 'lightweight ORM generator for mysql, java and optionally jsp/servlets - this utilises the templar templating language'
	tags = [ 'h2zero', 'ORM', 'generator' ]

	plugins {
		h2zeroPlugin {
			id = 'synapticloop.h2zero'
			displayName = 'Synapticloop Gradle h2zero plugin'
		}
	}
}
copyrightr {
	excludes [
		"src/test/java/synapticloop/sample/h2zero/**/*.*"
	]
	dryRun = false
}
task(dist).dependsOn( [ 'cobertura', 'jar', 'javadoc' ] )

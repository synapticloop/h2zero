package {database.package}.model;{\n}{\n}

// - - - - thoughtfully generated by synapticloop h2zero - - - - {\n}
//    with the use of synapticloop templar templating language{\n}
//                  (java-create-model.templar){\n}{\n}
{if !table.isConstant}
	import synapticloop.h2zero.base.manager.{options.database}.ConnectionManager;{\n}
	import synapticloop.h2zero.base.model.{options.database}.ModelBase;{\n}
	import synapticloop.h2zero.base.exception.H2ZeroPrimaryKeyException;{\n}
	import java.lang.StringBuilder;{\n}
	import java.sql.Connection;{\n}
{if fn:requiresImport[table, "Date"]}import java.sql.Date;{\n}{endif}
{if fn:requiresImport[table, "Clob"]}import java.sql.Clob;{\n}{endif}
{if fn:requiresImport[table, "Blob"]}import java.sql.Blob;{\n}{endif}
{if fn:requiresImport[table, "Timestamp"]}import java.sql.Timestamp;{\n}{endif}
{if fn:requiresImport[table, "Time"]}import java.sql.Time;{\n}{endif}
	import java.sql.ResultSet;{\n}
	import java.sql.Statement;{\n}
	import java.sql.PreparedStatement;{\n}
	import java.sql.SQLException;{\n}
	{\n}
{else}
	import java.util.HashMap;{\n}{\n}
	import java.util.Map;{\n}{\n}
{endif}

import {database.package}.model.util.Constants;{\n}

{if !table.isConstant}
	{\n}

	import {database.package}.finder.{table.javaClassName}Finder;{\n}

	{--
	  if this isn't a lookup on a foreign key and if the foreign key is not a constant
	  table, then we need to import the finder
	}
	{loop table.fields as field}
		{if fn:and[fn:notNull[field.foreignKeyTable], !field.foreignKeyTableLookup.isConstant]}
			import {database.package}.finder.{field.foreignKeyTableLookup.javaClassName}Finder;{\n}
		{endif}
	{endloop}

{endif}

{\n}{\n}

public class {table.javaClassName} {if !table.isConstant}extends ModelBase {endif}{{{\n}
{\t}// the binder is unused in code, but will generate compile problems if this {\n}
{\t}// class is no longer referenced in the h2zero file. Just a nicety for{\n}
{\t}// removing dead code{\n}
{\t}@SuppressWarnings("unused"){\n}
{\t}private static final String BINDER = Constants.{table.upperName}_BINDER;{\n}{\n}

{-- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
				CONSTANTS GENERATOR
}
{if table.isConstant}
	{loop table.constants as constant}
		{\t}public static final {table.javaClassName} {constant.name} = new {table.javaClassName}(
			{loop constant.values as value}
				{value}{if !valueStatus.last}, {endif}
			{endloop}
		);{\n}
	{endloop}
	{\n}

	{-- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
		Now add in the static array
	}

	{\t}public static final {table.javaClassName}[] ALL =  {{{\n}
		{\t}{\t}{loop table.constants as constant}
				{table.javaClassName}.{constant.name}{if !constantStatus.last}, {endif}
			{endloop}{\n}
	{\t}};{\n}
	{\n}

	{-- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
		And the static HashMap lookup
	}

	{\t}public static final Map<Long, {table.javaClassName}> ALL_LOOKUP = new HashMap<Long, {table.javaClassName}>();{\n}
	{\t}static{{{\n}
		{loop table.constants as constant}
			{\t}{\t}ALL_LOOKUP.put({constant.primaryKeyValue}, {table.javaClassName}.{constant.name});{\n}
		{endloop}{\n}
	{\t}};{\n}
	{\n}

{endif}

	{loop table.fields as field}
		{if field.primary}
			{\t}public static final String PRIMARY_KEY_FIELD = "{field.name}";{\n}{\n}
		{endif}
	{endloop}

{if !table.isConstant}
	{-- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
					SQL insert
	}

	{\t}private static final String SQL_INSERT = "insert into {table.name} values (

	{-- now loop through all of the fields }
	{loop table.fields as field}
		?
		{if fieldStatus.last}{else}, {endif}

	{endloop})";{\n}

	{-- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
					SQL update
	}

	{\t}private static final String SQL_UPDATE = "update {table.name} set 

	{-- now loop through all of the fields }
	{loop table.fields as field}
		{if field.primary}{else}
			{field.name} = ?
			{if fieldStatus.last}{else}, {endif}
		{endif}

	{endloop} where " + PRIMARY_KEY_FIELD + " = ?";{\n}

	{\t}private static final String SQL_DELETE = "delete from {table.name} where " + PRIMARY_KEY_FIELD + " = ?";{\n}

	{-- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
					INSERT IF NOT EXISTS SQL - ENSURE
	}
	{\t}private static final String SQL_ENSURE = "select " + PRIMARY_KEY_FIELD + " from {table.name} where 
	{loop table.fields as field}
		{if !field.primary}
			{field.name} = ?
			{if !fieldStatus.last} and {endif}
		{endif}
	{endloop}";{\n}
{\n}

{\t}private static final String SQL_SELECT_HYDRATE = "select 
{loop table.nonPopulateFields as field}
	{field.name}
	{if !fieldStatus.last}, {endif}
{endloop}
 from {table.name} where " + PRIMARY_KEY_FIELD + " = ?";{\n}

{\n}// Static lookups for fields in the hit counter.{\n}
	{\t}public static final int HIT_TOTAL = 0;{\n}
	{\t}public static final int HIT_PRIMARY_KEY = 0;{\n}
	{loop table.fields as field}
		{\t}public static final int HIT_{field.upperName} = {fieldStatus.index};{\n}
	{endloop}{\n}
	{\n}
	{\t}// the list of fields for the hit - starting with 'TOTAL'{\n}
	{\t}private static final String[] HIT_FIELDS = {{ "TOTAL", {loop table.fields as field}"{field.name}"{if fieldStatus.last}{else}, {endif}{endloop} };{\n}
	{\t}// the number of read-hits for a particular field{\n}
	{\t}private static int[] HIT_COUNTS = {{ 0, {loop table.fields as field}0{if fieldStatus.last}{else}, {endif}{endloop} };{\n}
	{set fn:length[table.nonPopulateFields] as nonPopulateFieldsLength}
	{\t}private boolean isHydrated = {if fn:=[nonPopulateFieldsLength, '0']}true{else}false{endif};{\n}
{endif}{-- end of !if.isConstant}
{\n}
	{-- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
					LIST ALL OF THE JAVA FIELDS
	}

	{loop table.fields as field}
		{if fn:notNull[field.foreignKeyTable]}
			{if !field.foreignKeyTableLookup.isConstant}
				{\t}private {field.foreignKeyTableLookup.javaClassName} {field.secondaryJavaName} = null;{\n}
			{endif}
		{endif}
		{if fieldStatus.last}
			{\n}
		{endif}
	{endloop}

	{loop table.fields as field}
		{\t}private {field.javaType} {field.javaName} = null;{\n}
	{endloop}
	{\n}


	{-- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
					CONSTRUCTORS
				what about null-able fields?
	}

	{\t}public {table.javaClassName}(
		{loop table.fields as field}
			{field.javaType} {field.javaName}
			{if fieldStatus.last}{else}, {endif}
		{endloop}
	) {{{\n}
		{loop table.fields as field}
			{\t}{\t}this.{field.javaName} = {field.javaName};{\n}
		{endloop}

	{\t}}{\n}

	{if table.hasNonNullConstructor}
	{\n}
		{\t}public {table.javaClassName}(
			{loop table.nonNullFields as field}
				{field.javaType} {field.javaName}
				{if fieldStatus.last}{else}, {endif}
			{endloop}
		) {{{\n}
			{loop table.fields as field}
				{if field.nullable}
					{\t}{\t}this.{field.javaName} = null;{\n}
				{else}
					{\t}{\t}this.{field.javaName} = {field.javaName};{\n}
				{endif}
			{endloop}

		{\t}}{\n}
	{endif}

{if !table.isConstant}
	{-- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
					PRIMARY KEY TEST AND SETTER/GETTER
	}

	{loop table.fields as field}
		{if field.primary}
			{\n}
			{\t}@Override{\n}
			{\t}public boolean primaryKeySet() {{{\n}
			{\t}{\t}return(null != {field.javaName});{\n}
			{\t}}{\n}
			{\n}
		{endif}
	{endloop}{\n}

	{-- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
				C(R)UD METHODS - WITHOUT THE READ
	}

	{\t}@Override{\n}
	{\t}public void insert(Connection connection) throws SQLException, H2ZeroPrimaryKeyException {{{\n}

	{\t}{\t}if(primaryKeySet()) {{{\n}
	{\t}{\t}{\t}throw new H2ZeroPrimaryKeyException("Cannot insert {table.name} model when primary key is not null.");{\n}
	{\t}{\t}}{\n}
	{\t}{\t}// create this bean {\n}
	{\t}{\t}PreparedStatement preparedStatement = connection.prepareStatement(SQL_INSERT, Statement.RETURN_GENERATED_KEYS);{\n}

	{loop table.fields as field}
		{\t}{\t}ConnectionManager.set{field.upperType}(preparedStatement, {fieldStatus.index}, {field.javaName});{\n}
	{endloop}

	{\t}{\t}preparedStatement.executeUpdate();{\n}

	{\t}{\t}ResultSet resultSet = preparedStatement.getGeneratedKeys();{\n}
	{\t}{\t}if(resultSet.next()) {{{\n}

	{loop table.fields as field}
		{if field.primary}
			{\t}{\t}{\t}this.{field.javaName} = resultSet.getLong(1);{\n}
		{endif}
	{endloop}

	{\t}{\t}} else {{{\n}
	{\t}{\t}{\t}throw new H2ZeroPrimaryKeyException("Could not get return value for primary key!");{\n}
	{\t}{\t}}{\n}


	{\t}{\t}ConnectionManager.closeAll(resultSet, preparedStatement);{\n}

	{\t}}{\n}
	{\n}

	{--
	    ENSURE
	}
	{\t}@Override{\n}
	{\t}public void ensure(Connection connection) throws SQLException, H2ZeroPrimaryKeyException {{{\n}

	{\t}{\t}PreparedStatement preparedStatement = connection.prepareStatement(SQL_ENSURE);{\n}

	{loop table.nonPrimaryFields as field}
		{\t}{\t}ConnectionManager.set{field.upperType}(preparedStatement, {fieldStatus.index}, {field.javaName});{\n}
	{endloop}

	{\t}{\t}ResultSet resultSet = preparedStatement.executeQuery();{\n}

	{\t}{\t}if(resultSet.next()) {{{\n}

	{loop table.fields as field}
		{if field.primary}
			{\t}{\t}{\t}this.{field.javaName} = resultSet.getLong(1);{\n}
		{endif}
	{endloop}

	{\t}{\t}} else {{{\n}
	{\t}{\t}{\t}// could not find the value - need to insert it - null is the primary key{\n}
	{\t}{\t}{\t}insert(connection);{\n}
	{\t}{\t}}{\n}


	{\t}{\t}ConnectionManager.closeAll(resultSet, preparedStatement);{\n}

	{\t}}{\n}
	{\n}

	{\t}@Override{\n}
	{\t}public void update(Connection connection) throws SQLException, H2ZeroPrimaryKeyException {{{\n}

	{\t}{\t}if(!primaryKeySet()) {{{\n}
	{\t}{\t}{\t}throw new H2ZeroPrimaryKeyException("Cannot update bean when primary key is null.");{\n}
	{\t}{\t}}{\n}

	{\t}{\t}if(isDirty) {{{\n}
	{\t}{\t}{\t}// update this bean, but only if dirty{\n}
	{\t}{\t}{\t}PreparedStatement preparedStatement = connection.prepareStatement(SQL_UPDATE);{\n}

	{set fn:length[table.fields] as preparedStatementSize}

	{loop table.fields as field}
		{if !field.primary}
			{\t}{\t}{\t}ConnectionManager.set{field.upperType}(preparedStatement, {fieldStatus.offset}, {field.javaName});{\n}
		{endif}
	{endloop}

	{\t}{\t}{\t}// now set the primary key{\n}
	{loop table.fields as field}
		{if field.primary}
			{\t}{\t}{\t}preparedStatement.setLong({preparedStatementSize}, {field.javaName});{\n}
		{endif}
	{endloop}

	{\t}{\t}{\t}preparedStatement.executeUpdate();{\n}
	{\t}{\t}{\t}ConnectionManager.closeAll(preparedStatement);{\n}
	{\t}{\t}{\t}isDirty = false;{\n}
	{\t}{\t}}{\n}
	{\t}}{\n}
	{\n}


	{\t}@Override{\n}
	{\t}public void delete(Connection connection) throws SQLException, H2ZeroPrimaryKeyException{{{\n}

	{\t}{\t}if(!primaryKeySet()) {{{\n}
	{\t}{\t}{\t}throw new H2ZeroPrimaryKeyException("Cannot delete bean when primary key is null.");{\n}
	{\t}{\t}}{\n}
	{\t}{\t}PreparedStatement preparedStatement = connection.prepareStatement(SQL_DELETE);{\n}
	{loop table.fields as field}
		{if field.primary}
			{\t}{\t}preparedStatement.setLong(1, {field.javaName});{\n}
		{endif}
	{endloop}
	{\t}{\t}preparedStatement.executeUpdate();{\n}
	{\t}{\t}ConnectionManager.closeAll(preparedStatement);{\n}
	{\t}}{\n}
	{\n}


	{\t}@Override{\n}
	{\t}public void refresh(Connection connection) throws H2ZeroPrimaryKeyException {{{\n}

	{\t}{\t}if(!primaryKeySet()) {{{\n}
	{\t}{\t}{\t}throw new H2ZeroPrimaryKeyException("Cannot refresh bean when primary key is null.");{\n}
	{\t}{\t}}{\n}
	{loop table.fields as field}
		{if field.primary}
			{\t}{\t}{table.javaClassName} {table.javaFieldName} = {table.javaClassName}Finder.findByPrimaryKeySilent(connection, this.{field.javaName});{\n}
		{endif}
	{endloop}

		{loop table.fields as field}
			{\t}{\t}this.{field.javaName} = {table.javaFieldName}.get{field.javaAccessorName}();{\n}
		{endloop}

		{--
			Now we need to go through all of the constant fields and set them to null
		}

	{loop table.fields as field}
		{if fn:notNull[field.foreignKeyTable]}
			{if !field.foreignKeyTableLookup.isConstant}
				{\t}{\t}this.{field.secondaryJavaName} = null;{\n}
			{endif}
		{endif}
	{endloop}

	{\t}}{\n}
	{\n}

	{set fn:length[table.nonPopulateFields] as nonPopulateFieldsLength}
	{if !fn:=[nonPopulateFieldsLength, '0']}

		{loop table.fields as field}
			{if field.primary}
				{set field.name as primaryFieldName}
				{set field.javaName as primaryFieldJavaName}
			{endif}
		{endloop}

		{\t}@Override{\n}
		{\t}protected void hydrate(Connection connection) throws SQLException, H2ZeroPrimaryKeyException {{{\n}

		{\t}{\t}if(!primaryKeySet()) {{{\n}
		{\t}{\t}{\t}throw new H2ZeroPrimaryKeyException("Cannot hydrate bean when primary key is null.");{\n}
		{\t}{\t}}{\n}
		{\t}{\t}PreparedStatement preparedStatement = null;{\n}
		{\t}{\t}ResultSet resultSet = null;{\n}

		{\t}{\t}try {{{\n}
		{\t}{\t}{\t}preparedStatement = connection.prepareStatement(SQL_SELECT_HYDRATE);{\n}
		{\t}{\t}{\t}preparedStatement.setLong(1, {primaryFieldJavaName});{\n}
		{\t}{\t}{\t}resultSet = preparedStatement.executeQuery();{\n}

		{\t}{\t}{\t}if(!resultSet.next()) {{{\n}
		{\t}{\t}{\t}{\t}throw new H2ZeroPrimaryKeyException("Could not find result with primary key of: " + getPrimaryKey());{\n}
		{\t}{\t}{\t}}{\n}

		{loop table.nonPopulateFields as field}
			{\t}{\t}{\t}this.{field.javaName} = ConnectionManager.getNullableResult{field.sqlJavaType}(resultSet, {fieldStatus.index});{\n}
		{endloop}

		{\t}{\t}} catch (SQLException sqlex) {{{\n}
		{\t}{\t}{\t}throw sqlex;{\n}
		{\t}{\t}} finally {{{\n}
		{\t}{\t}{\t}ConnectionManager.closeAll(resultSet, preparedStatement);{\n}
		{\t}{\t}}{\n}
		{\n}
		{\t}}{\n}

	{endif}


	{\t}public static String[] getHitFields() {{ return(HIT_FIELDS); }{\n}
	{\t}public static int[] getHitCounts() {{ return(HIT_COUNTS); }{\n}{\n}

{endif}{-- end of !isconstant table }

	{-- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
					SETTERS AND GETTERS

					The first getter is for any referenced foreign key tables that exist
					which will get the actual object, rather than the primary key that 
					references it.
	}

	{loop table.fields as field}
		{if fn:notNull[field.foreignKeyTable]}
			{\t}public {field.foreignKeyTableLookup.javaClassName} get{field.secondaryJavaName}() {{{\n}
			{if !field.foreignKeyTableLookup.isConstant}
				{\t}{\t}if(null == this.{field.secondaryJavaName}) {{{\n}

				{\t}{\t}{\t}this.{field.secondaryJavaName} = {field.foreignKeyTableLookup.javaClassName}Finder.findByPrimaryKeySilent(this.{field.javaName});{\n}

				{\t}{\t}}{\n}

				{\t}{\t}return(this.{field.secondaryJavaName});{\n}
			{else}

				{--
					At this point the foreign table is constant so it was already looked up
					at construction time
				}

				{\t}{\t}return({field.foreignKeyTableLookup.javaClassName}.ALL_LOOKUP.get(this.{field.javaName}));{\n}
			{endif}

			{\t}}{\n}{\n}
		{endif}
	{endloop}

	{if !table.isConstant}
		{\t}public static void updateHitCount(int offset) {{{\n}
		{\t}{\t}HIT_COUNTS[0]++;{\n}
		{\t}{\t}HIT_COUNTS[offset]++;{\n}
		{\t}}{\n}{\n}
	{endif}

	{\t}/*{\n}
	{\t} * Boring ol' getters and setters {\n}
	{\t} */{\n}
	{\n}

	{loop table.fields as field}
		{if field.primary}
		{\t}public {field.javaType} getPrimaryKey() {{ 

			{if !table.isConstant}
				updateHitCount({fn:+[field.fieldIndex, '1']}); 
			{endif}

			return(this.{field.javaName}); 
		}{\n}
		{endif}
	{endloop}

	{loop table.fields as field}
		{if field.primary}
			{if !table.isConstant}
				{\t}public void setPrimaryKey({field.javaType} {field.javaName}) {{ 
					if(isDifferent(this.{field.javaName}, {field.javaName})) {{ 
						this.{field.javaName} = {field.javaName};
						this.isDirty = true; 

						{if fn:notNull[field.foreignKeyTable]}
							{if !field.foreignKeyTableLookup.isConstant}
								this.{field.secondaryJavaName} = null;
							{endif}
						{endif}

					}
				}{\n}
			{endif}
		{endif}
	{endloop}

	{loop table.fields as field}
		{\t}public {field.javaType} get{field.javaAccessorName}() {{ 

			{if !table.isConstant}
				updateHitCount({fn:+[field.fieldIndex, '1']}); 
			{endif}

			{--
				We need to check for hydration
			}
			{if !field.populate}
			if(!isHydrated) {{ hydrateSilent(); this.isHydrated = true; } 
			{endif}

			return(this.{field.javaName}); 
		}{\n}

		{if !table.isConstant}
			{\t}public void set{field.javaAccessorName}({field.javaType} {field.javaName}) {{ 
				if(isDifferent(this.{field.javaName}, {field.javaName})) {{ 
					this.{field.javaName} = {field.javaName};
					this.isDirty = true; 

					{if fn:notNull[field.foreignKeyTable]}
						{if !field.foreignKeyTableLookup.isConstant}
							this.{field.secondaryJavaName} = null;
						{endif}
					{endif}

				}
			}{\n}
		{endif}

	{endloop}
	{-- Now we need the toString Method--}
	{\n}
	{\t}@Override{\n}
		{\t}public String toString() {{{\n}
		{\t}{\t}StringBuilder stringBuilder = new StringBuilder();{\n}

		{\t}{\t}stringBuilder.append("Model[{table.javaClassName}]\n");{\n}
		{loop table.fields as field}
			{if field.isSecure}
				{\t}{\t}stringBuilder.append("  Field[{field.javaName}:<**secure**>]\n");{\n}
			{else}
				{\t}{\t}stringBuilder.append("  Field[{field.javaName}:" + this.{field.javaName} + "]\n");{\n}
			{endif}
		{endloop}

		{\t}{\t}return(stringBuilder.toString());{\n}
		{\t}}{\n}


	{-- Now we need the toJsonString Method--}
	{\n}
		{\t}public String toJsonString() {{{\n}
		{\t}{\t}StringBuilder stringBuilder = new StringBuilder();{\n}

		{\t}{\t}stringBuilder.append("{{\n");{\n}
		{\t}{\t}stringBuilder.append("  \"type\": \"{table.javaClassName}\",\n");{\n}

		{loop table.fields as field}
			{if field.isSecure}
				{\t}{\t}stringBuilder.append("  \"{field.javaName}\": \"<**secure**>\", \n");{\n}
			{else}
				{\t}{\t}stringBuilder.append("  \"{field.javaName}\": {if field.shouldEscape}\"{endif}" + this.{field.javaName} + "{if field.shouldEscape}\"{endif} {if !fieldStatus.last}, {endif}\n");{\n}
			{endif}
		{endloop}

		{\t}{\t}stringBuilder.append("}\n");{\n}

		{\t}{\t}return(stringBuilder.toString());{\n}
		{\t}}{\n}
		{\n}

	{-- and the getJSONString - for chaining --}
	{\t}public String getJsonString() {{{\n}
	{\t}{\t}return(toJsonString());{\n}
	{\t}}{\n}

{if !table.isConstant}
	{-- This is the hitcount --}
	{\n}
		{\t}public static String getHitCountJson() {{{\n}
		{\t}{\t}StringBuilder stringBuilder = new StringBuilder();{\n}

		{\t}{\t}stringBuilder.append("{{\n");{\n}
		{\t}{\t}stringBuilder.append("  \"type\": \"{table.name}\",\n");{\n}

		{\t}{\t}stringBuilder.append("  \"total\": " + HIT_COUNTS[0] + ", \n");{\n}
		{loop table.fields as field}
			{\t}{\t}stringBuilder.append("  \"{field.name}\": " + HIT_COUNTS[{fn:+[field.fieldIndex, '1']}] + "{if !fieldStatus.last}, {endif}\n");{\n}
		{endloop}

		{\t}{\t}stringBuilder.append("}\n");{\n}

		{\t}{\t}return(stringBuilder.toString());{\n}
		{\t}}{\n}
		{\n}
{endif}
}

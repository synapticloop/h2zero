--        - - - - thoughtfully generated by synapticloop h2zero - - - -        {\n}
--          with the use of synapticloop templar templating language{\n}
--                 (/sql/sql-create-database-sqlite3.templar){\n}{\n}

{--
	Loop through all of the tables to write out the creation definition
}
{loop database.tables as table}
	{loop table.comments as comment}
		{if commentStatus.first}
			--{\n}
		{endif}
		-- {comment}{\n}
		{if commentStatus.last}
			--{\n}
		{endif}
	{endloop}

	drop table if exists {table.name};{\n}
	
	create table {table.name} ({\n}
	{--
		Now loop through each of the field definitions
	}

	{set fn:length[table.fields] as fieldsLength}
	{loop table.fields as field}
		
		{if field.primary}
			{\t}{field.name} INTEGER
		{else}
			{\t}{field.name} {field.type}{field.lengthFormat}
		{endif}
		{if !field.nullable} not{endif} null
		{if fn:notNull[field.default]} default {field.default}{endif}
		{if field.primary} PRIMARY KEY AUTOINCREMENT{endif}
		{if fn:not=[fieldStatus.index, fieldsLength]},{\n}{endif}
	{endloop}


	{--
		foreign key definitions
	}
	{loop table.fields as field}
		{if fn:notNull[field.foreignKeyTable]}
			{if fn:notNull[field.foreignKeyField]}
				,{\n}{\t}foreign key ({field.name}) references {field.foreignKeyTable} ({field.foreignKeyField})
				{if fn:notNull[field.onDelete]} ON DELETE {field.onDelete}{endif}
				{if fn:notNull[field.onUpdate]} ON UPDATE {field.onUpdate}{endif}
			{endif}
		{endif}
	{endloop}

{\n}
	);{\n}{\n}

	{--
		Unique key definitions
	}
	{loop table.fields as field}
		{if field.unique}
			create unique index {table.name}_{field.name}_idx_unq on {table.name}({field.name});{\n}
		{endif}
	{endloop}

	{--
		index definitions
	}

	{loop table.fields as field}
		{if field.index}
			create index {table.name}_{field.name}_idx on {table.name}({field.name});{\n}
		{endif}
	{endloop}

{\n}
{\n}


	{if table.isConstant}
		-- The {table.name} table is defined as being constant{\n}
		-- insert the values{\n}{\n}

		{loop table.constants as constant}
			insert into {table.name} values({loop constant.sqlValues as value}{value}{if !valueStatus.last}, {endif}{endloop});{\n}
		{endloop}
	{\n}
	{\n}

	{endif}

{endloop}



{loop database.views as view}

drop view if exists {view.name};{\n}{\n}

create view {view.name} as {view.asClause};{\n}{\n}
{endloop}